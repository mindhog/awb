import crack.cont.array Array;
import crack.cont.hashmap HashMap;
import crack.ext._jack JackClient;
import crack.functor Functor1, Functor2;
import crack.io cerr, FileHandle;
import crack.lang Exception, InvalidStateError;
import crack.logger debug, error, info;
import crack.math floor;
import crack.midi.alsa Port, Sequencer, SND_SEQ_OPEN_OUTPUT,
    SND_SEQ_OPEN_INPUT;
import crack.midi.event ChannelEvent, ControlChange, EndTrack, Event,
    MidiWriter, Track;
import crack.net Pipe, Poller, PollEvent, PollEventCallback, POLLERR,
    POLLHUP, POLLOUT, POLLIN;
import crack.rawptr RawPtr;
import crack.runtime sleep, PollEvt, PollSet;
import crack.threads Mutex, MutexLock, Thread;
import crack.time TimeDelta;
import spug.loop.jack JackPos, JackWrapper, WaveManager, JACK_TRANSPORT_ROLLING;
import spug.loop.model EventManager, LoopCursor, Section;

@import crack.ann assert, cvars, impl, interface, struct;

@export_symbols TimeCallback;

class MidiSequencer;

## EventProcessors are called whenever the MidiEngine receives an event.  They
## are called synchronously and therefore must be able to deal with the event
## in a small bounded amount of time.
alias EventProcessor = Functor2[void, MidiSequencer, Event];

const MAX_TIME := 0xffffffff;

# TODO:
# - Keep track of what notes are playing, silence them all on stop.

# Engine states.
const int
    ST_IDLE = 0,
    ST_PLAY = 1,

    ## Latched for record (first event will start the recording)
    ST_LATCHED = 2,
    ST_RECORD = 3;

## Interface to wrap jack interactions.
@interface Jack {
    @abstract void start();
    @abstract void stop();
    @abstract int getTransportState(JackPos pos);

    ## Set the current time position to 'secs' seconds since the beginning of
    ## all tracks.
    @abstract void setTime(float secs);

    ## Returns the current time.
    @abstract float getTime();

    ## True if there are any tracks in the wave manager, false if it's empty.
    @abstract bool hasTracks();
}

## Interface for things that watch the MidiSequencer's model state.
@interface ModelObserver {
    @abstract void addTrack(Track track);
}

Track cloneTrack(Track track) {
    result := Track(track.name);
    for (event :in track)
        result.add(event);
    return result;
}

class Interrupter @impl PollEventCallback {
    Pipe __pipe = {};

    int oper call(Poller poller, PollEvent event) {
        if (event.revents & POLLIN) {
            __pipe.read(1024);
        } else if (event.revents) {
            error `Interrupter pipe error`;
            __pipe.close();
            poller.remove(__pipe);
        }

        return POLLIN | POLLERR | POLLHUP;
    }

    void registerHandlers(Poller poller) {
        poller.add(__pipe, this);
    }

    void signal() {
        __pipe.write('x');
    }
}

# Time events are sent periodically from the midi sequencer (no less than
# every 0.1 second when the sequencer is active).
@struct TimeEvent {

    ## Time in microseconds since the start of the current section.
    uint64 usecs;

    ## Time in midi ticks since the start of the current section.
    uint ticks;
}

alias TimeCallback = Functor1[void, TimeEvent];

@final class MidiSequencer {

    # If this is specified, the channel of all input events is rewritten to
    # the value specified.
    int inputChannel = -1;

    Sequencer __seq;
    Jack __jack;

    # Last known state information of the jack transport.
    JackPos __pos = JackPos();
    int __transportState;
    # True if __pos and __transportState are valid.
    bool __posIsValid;

    EventProcessor __processor;
    ModelObserver __modelObserver;

    ## The track we're currently recording.  If this is non-null and the
    ## transport is rolling, we're recording.  Otherwise we're not.
    Track __recordTrack;

    Section __section = Section();

    Array[LoopCursor] __trackCursors;

    ## Start time of the transport in microseconds (when we started jack
    ## rolling).
    ## This is currently also the start time of the first play of the section
    ## in microseconds, which is really more what we want.
    uint64 __startTime;

    ## The jack time (in microseconds) where we last went into idle mode.
    uint64 __lastTime;

    ## Current engine state (see the ST_* constants above).
    int __state;

    ## Mutex controlling all parameters that can be set by MidiEngine.
    Mutex __lock = Mutex();

    ## Pulses per beat.  Note that this can only be set when there are
    ## no tracks.
    int __ppb = 1440;

    ## We're currently doing this based on 60bpm (1 bps) where ticks per beat
    ## is 5 * 9 * 32 (1440).
    float __ticksPerSecond = 1440;

    Interrupter __interrupter = {};

    # The subscriber callbacks.
    HashMap[uintz, TimeCallback] __timeCallbacks = {};

    # Temporary hack so I can get ports working.
    Port outPort;

    ## Updates the variables that we use to store the current jack state
    ## (initially this is only the "transport state" and position.
    @final void __updateJackState() {
        if (!__posIsValid) {
            lastState := __transportState;
            __transportState = __jack.getTransportState(__pos);
            __posIsValid = true;
        }
    }

    ## Return the time in midi ticks relative to the beginning of the last
    ## iteration of the section.
    ##
    ## This relies on the validity of the current time position structure as
    ## determined by the processing loop.
    @final uint __getTicks(uint span) {
        __updateJackState();
        absTicks := uint(__ticksPerSecond * float(__pos.usecs - __startTime) /
                         1000000.0
                         );
        if (span)
            absTicks = absTicks % span;
        return absTicks;
    }

    ## Returns the number of microseconds necessary for the specified number
    ## of ticks.
    @final uint64 __ticksToUsecs(uint ticks) {
        return uint64(1000000.0 * float(ticks) / __ticksPerSecond);
    }

    @final uint __usecsToTicks(uint64 usecs) {
        return uint((float(usecs) / 1000000.0) * __ticksPerSecond);
    }

    ## Compute the new start time to use at the beginning of a record.
    ## This modifies __startTime to be the new start time.
    @final void __computeStartTime() {
        if (!__startTime || !__section.span) {
            # We haven't yet set start time or don't yet have a span, so just
            # use the current time as the start time.
            __startTime = __pos.usecs;
        } else {
            spanInSeconds := float(__section.span) / __ticksPerSecond;

            # Calculate the number of cycles that have elapsed since the last
            # start time.
            cycles := float(__pos.usecs - __startTime) / 1000000.0 /
                      spanInSeconds;

            # The new start time is the beginning of the last cycle
            __startTime += uint64(floor(cycles) * spanInSeconds * 1000000.0);
        }
    }

    @final void __initializeTrackCursors() {
        __trackCursors = Array[LoopCursor](__section.tracks.count());
        debug `midieng: initializing track cursors`;
        for (track :in __section.tracks) {
            __trackCursors.append(cur := LoopCursor(track));
            cur.setPos(pos := __getTicks(cur.span));
            debug `midieng: track $(track.name) set to position $pos`;
        }
    }

    ## Set all track cursors so that the next event is the event to be played
    ## at or after 'pos'.  'pos'
    @final void __setTrackCursors(uint pos) {
        debug `midieng: setting track cursors to $pos`;
        # Recreate the track cursors (this should be refactored out).
        __trackCursors = Array[LoopCursor](__section.tracks.count());
        for (track :in __section.tracks)
            __trackCursors.append(LoopCursor(track));

        for (cur :in __trackCursors) {
            # Find the first event that is after 'pos'.
            startIndex := cur.index;
            if (!cur.span) {
                error `track $(cur.track.name) has a zero length span`;
                continue;
            }
            while (cur.elem().time < pos % cur.span) {
                cur.next();
                if (cur.index == startIndex) {
                    # XXX doesn't look like we ever actually see this.
                    break;
                }
            }
            debug I`midieng: set cursor for track $(cur.track.name) to \
                    event at $(cur.elem().time)`;
        }
    }

    void __playPendingEvents(LoopCursor cur, uint pos) {
        while (cur && !cur.immediatelyFollows(pos % cur.span)) {
            # Send it off to the processor (if it's not an EndTrack)
            evt := cur.next();
            if (!EndTrack.cast(evt, null)) {
                info `midieng: playing event $evt`;
                __processor(this, evt);
            }
        }
    }

    void __beginRecord() {
        @assert(!__recordTrack);
        # Create a record track, make sure the transport is rolling.
        info `midieng: Starting record`;
        __recordTrack = Track('midi-record-track');
        if (__transportState != JACK_TRANSPORT_ROLLING) {
            __jack.start();
            __updateJackState();
        }
        __computeStartTime();
        # If there are no tracks here or in jack, we started out in "latched"
        # state, otherwise go directly to "record".  TODO: maybe allow the
        # user to disable latched in the case where they are just recording a
        # midi track.
        __state = (__section.span || __jack.hasTracks()) ? ST_RECORD :
                                                           ST_LATCHED;
    }

    void __endRecord() {
        info `midieng: saving recorded track`;
        t := __getTicks(0);
        __section.markEnding(__recordTrack, t, true);
        __section.tracks.append(__recordTrack);

        # Until project model gets unified across the old and new
        # systems, we need to copy the track and send it to the
        # event manager.
        if (__modelObserver)
            __modelObserver.addTrack(cloneTrack(__recordTrack));

        __recordTrack = null;
        __state = ST_PLAY;

        # Update the track cursors now that we have a new one, then
        # deal with any events that now need to be played immediately.
        __setTrackCursors(__getTicks(__section.span));
        for (cur :in __trackCursors)
            __playPendingEvents(cur, t);
    }

    @final void __processEvent(Event event) {
        if (inputChannel > -1 && event := ChannelEvent.cast(event, null))
            event.channel = byte(inputChannel);

        # Some hacks to get everything working.
        if (cc := ControlChange.cast(event, null)) {

            # CC 45 is the "record" button for the Korg nanokontrol.  So
            # basically this clause starts midi recording (and switches to
            # play mode if we're already recording)
            if (cc.controller == 45) {

                # Ignore "button up" events.
                if (cc.value != 127)
                    return;

                if (__recordTrack)
                    __endRecord();
                else
                    __beginRecord();

                return;
            } else if (cc.controller == 43) {
                # Temporarily binding nano's "<<" key to something that clears
                # everything out.
                __recordTrack = null;
                __section = Section();
                __startTime = 0;
                __trackCursors = null;
                __jack.stop();
                __jack.setTime(0);
                return;
            } else if (cc.controller == 41) {
                # Control 41 is the "play" button, so have it toggle play.
                if (cc.value != 127)
                    return;
                if (__transportState == JACK_TRANSPORT_ROLLING) {
                    # Currently playing.  Stop.
                    __jack.stop();
                    __state = ST_IDLE;
                    __startTime = 0;
                } else {
                    __jack.start();
                    __jack.setTime(0);
                    __startTime = __pos.usecs;
                    __setTrackCursors(0);
                    for (cur :in __trackCursors)
                        __playPendingEvents(cur, /* ticks = */ 0);
                }
                return;
            }
        }

        if (__processor)
            __processor(this, event);

        # This probably belongs in the event processor.
        if (!(__recordTrack is null)) {

            # If we don't currently have any events in the record track,
            # this is the first event and we want to recompute the start
            # time and then reset the event time to the new start time.
            if (__state == ST_LATCHED) {
                __computeStartTime();
                event.time = __getTicks(0);
                __state = ST_RECORD;
            }

            __recordTrack.append(event);
        }
    }

    oper init(Jack jack, Sequencer seq) :
        __jack = jack,
        __seq = seq {
    }

    class EventHandler @impl PollEventCallback {
        Sequencer __seq;
        RawPtr[MidiSequencer] __engine;

        oper init(MidiSequencer engine, Sequencer seq) :
            __engine = RawPtr[MidiSequencer](engine),
            __seq = seq {
        }

        int oper call(Poller poller, PollEvent evt) {
            while (__seq.hasEvent()) {
                evt := __seq.getEvent();
                if (evt) {
                    evt.time = __engine.__getTicks(0);
                    info `got midi event $evt, time is $(evt ? evt.time : 0)`;
                    __engine.__processEvent(evt);
                } else {
                    info `got null event`;
                }
            }
            return POLLIN;
        }
    }

    ## timeout: this is neither an input nor an output parameter, we just
    ## pass one of these in from the caller to avoid having to allocate one
    ## for each cycle.
    void runOneCycle(Poller poller, TimeDelta timeout) {
        lk := MutexLock(__lock);
        __updateJackState();

        if (__state != ST_IDLE) {
            # Get the current tick.
            tick := __getTicks(__section.span);

            # Send a time event.
            if (__timeCallbacks) {
                event := TimeEvent(__pos.usecs - __startTime, tick);
                for (item :in __timeCallbacks)
                    item.val(event);
            }

            # See if we need to play anything.
            if (__section.tracks) {
                # if we don't currently have a set of cursors, generate
                # them now.
                if (!__trackCursors) {
                    __initializeTrackCursors();
                }

                # Play all events that are due to be played.
                uint nextEventTime = MAX_TIME;
                for (cur :in __trackCursors) {
                    # safety check
                    if (!cur.span)
                        continue;

                    # play all events up to that point
                    __playPendingEvents(cur, tick);

                    # Figure out how many ticks until the next event
                    # (we do the span offsetting to deal with events that
                    # happen in the next iteration of the loop)
                    spanTick := tick % cur.span;
                    diff := (cur.elem().time < spanTick ? cur.span : 0) +
                            cur.elem().time - spanTick;
                    if (diff < nextEventTime)
                        nextEventTime = diff;
                }

                # If we need to schedule a next event time for playback,
                # set the timeout TimeDelta structure accordingly.
                if (nextEventTime < MAX_TIME) {
                    # Figure out the duration of a tick.
                    delta := float(nextEventTime) / __ticksPerSecond;
                    timeout.secs = int32(delta);
                    timeout.nsecs = int32((delta - floor(delta)) *
                                             1000000000.0);
                } else {
                    # Trigger an event for 0.1 seconds from now.
                    timeout.secs = 0;
                    timeout.nsecs = 100000000;
                }
            }
        }

        # We're going to wait now, so invalidate the jack transport state.
        __posIsValid = false;
        lk = null;
        poller.waitAndProcess(timeout);
    }

    void registerHandlers(Poller poller) {
        count := __seq.seq.poll_descriptors_count(POLLIN);
        fds := PollSet(count);
        rc := __seq.seq.poll_descriptors_vp(fds, count, POLLIN);

        evt := PollEvt();
        for (int i = 0; i < count; ++i) {
            fds.get(i, evt);
            debug `fd $(evt.fd) events $(evt.events)`;
            poller.add(FileHandle(evt.fd), EventHandler(this, __seq));
        }

        __interrupter.registerHandlers(poller);
    }

    void _run() {
        # Create the event handlers for incoming MIDI events.
        poller := Poller();
        registerHandlers(poller);

        # Set the time of the wave audio to the beginning.
        __jack.setTime(0);

        # This is the TimeDelta structure that we'll populate and reuse for
        # the poll timeout.  If the transport is rolling and there are events,
        # this will be populated with the time of the next event and 'timeout'
        # will be pointed at this object.  Otherwise, we'll use a timeout of
        # null.
        timeoutObj := TimeDelta();

        # The main event loop.
        while (true) {
            try {
                runOneCycle(poller, timeoutObj);
            } catch (Exception ex) {
                error `Got exception in run thread:\n$ex`;
            }
        }
    }

    # All of the following methods are part of the public interface of the
    # engine which can be called by any thread.  They need to be
    # reimplemented to queue events to be processed by the engine thread.

    ## Add a processor to the midi engine.  This will get called whenever an
    ## event is received.
    void addProcessor(EventProcessor processor) {
        # This is totally wrong, should be adding a processor to the processor
        # chain.
        lk := MutexLock(__lock);
        __processor = processor;
    }

    ## Sets the current model observer, which is notified of changes to the
    ## model object.
    void setModelObserver(ModelObserver observer) {
        lk := MutexLock(__lock);
        __modelObserver = observer;
    }

    void setPPB(int ppb) {
        lk := MutexLock(__lock);
        if (__section.tracks)
            throw InvalidStateError();
        __ppb = ppb;
    }

    void setBPM(float bpm) {
        lk := MutexLock(__lock);
        __ticksPerSecond = float(__ppb) * bpm / 60.0;
    }

    ## Toggle record mode.
    void toggleRecord() {
        lk := MutexLock(__lock);
        if (__recordTrack)
            __endRecord();
        else
            __beginRecord();
        __interrupter.signal();
    }

    ## Toggle play mode.
    void togglePlay() {
        lk := MutexLock(__lock);
        if (__recordTrack)
            __endRecord();

        __state = __state == ST_IDLE ? ST_PLAY : ST_IDLE;
        if (__state == ST_PLAY) {
            __posIsValid = false;
            __updateJackState();

            __startTime += __pos.usecs - __lastTime;
            __jack.start();

        } else {
            __jack.stop();

            __posIsValid = false;
            __updateJackState();
            __lastTime = __pos.usecs;
        }
        __interrupter.signal();
    }

    ## Set the current position (specified in ticks).
    void setPos(int pos) {
        lk := MutexLock(__lock);
        __setTrackCursors(pos);

        # Force a reload of jack state so we can reset the start position to
        # the current time.
        __posIsValid = false;
        __updateJackState();
        __startTime = __pos.usecs - __ticksToUsecs(uint(pos));
        __interrupter.signal();
    }

    ## Set the current section.  Note that once the Section has been set it
    ## basically becomes a shared resource and should really be treated
    ## as immutable.
    void setSection(Section section) {
        lk := MutexLock(__lock);
        __section = section;

        # Fix the track cursors so that the current position is converted to
        # the new section.
        uint64 t;
        if (__state == ST_IDLE) {
            # Transport is IDLE.  Use lastTime - startTime.
            t = __lastTime - __startTime;
        } else {
            # Transport is rolling.  Use current time - startTime.
            __updateJackState();
            t = __pos.usecs - __startTime;
        }
        __setTrackCursors(__usecsToTicks(t) % __section.span);

        __interrupter.signal();
    }

    void subscribeTimeEvent(TimeCallback callback) {
        lk := MutexLock(__lock);
        __timeCallbacks[uintz(callback)] = callback;
        __interrupter.signal();
    }
}

@final class JackImpl @impl Jack {
    @cvars {
        WaveManager __waveMan;
    }
    void start() {
        __waveMan.getClient().transportStart();
        __waveMan.unpause();
    }

    void stop() {
        __waveMan.getClient().transportStop();
        __waveMan.pause();
    }

    int getTransportState(JackPos pos) {
        return __waveMan.getClient().transportQuery(pos);
    }

    void setTime(float secs) {
        __waveMan.seek(int(float(__waveMan.getSampleRate()) * secs));
    }

    float getTime() {
        return float(__waveMan.tell()) / float(__waveMan.getSampleRate());
    }

    bool hasTracks() {
        return __waveMan.hasTracks();
    }
}

## Midi engine public interface.
@final class MidiEngine {
    MidiSequencer __mseq;

    oper init(WaveManager waveMan, Sequencer seq) :
        __mseq(JackImpl(waveMan), seq) {
    }

    void start() {
        class MyThread : Thread {
            @cvars {
                MidiSequencer eng;
            }

            void run() {
                eng._run();
            }
        }

        MyThread(__mseq).start();
    }

    void addProcessor(EventProcessor processor) {
        __mseq.addProcessor(processor);
    }

    void setModelObserver(ModelObserver observer) {
        __mseq.setModelObserver(observer);
    }

    ## Set pulses per beat.  Must be called prior to adding any tracks.  If
    ## there are already tracks, the PPB of the tracks is that of the engine.
    void setPPB(int ppb) {
        __mseq.setPPB(ppb);
    }

    ## Set the tempo in beats per minute.
    void setBPM(float bpm) {
        __mseq.setBPM(bpm);
    }

    ## Toggle record mode.
    void toggleRecord() {
        __mseq.toggleRecord();
    }

    ## Toggle play mode.
    void togglePlay() {
        __mseq.togglePlay();
    }

    ## Set the current position.
    void setPos(int pos) {
        __mseq.setPos(pos);
    }

    ## Set the current section.
    void setSection(Section section) {
        __mseq.setSection(section);
    }

    ## Subscribe to the time callback.
    void subscribeTimeEvent(TimeCallback callback) {
        __mseq.subscribeTimeEvent(callback);
    }

    Port oper .outPort =(Port port) { return __mseq.outPort = port }

    int oper .inputChannel =(int channel) {
        return __mseq.inputChannel = channel;
    }
}

class MidiDispatcher @impl EventProcessor {
    void oper call(MidiSequencer engine, Event event) {
        engine.outPort.send(event);
    }
}

## Dispatch to the event manager.
##
## This is provided as an adapter so I can continue to use the event manager
## functionality in applications that don't use EventManager (which is driven
## from the UI loop).  It would be better to get rid of the EventManager
## entirely and move its functionality to midi engine.
class EventManagerDispatcher @impl EventProcessor {
    @cvars {
        EventManager __eventManager;
    }

    void oper call(MidiSequencer engine, Event event) {
        __eventManager.onInputEvent(event);
    }
}
